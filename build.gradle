//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version "1.6.0"
	id 'org.quiltmc.quilt-mappings-on-loom' version "4.0.0"
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shade
}

repositories {
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://maven.blamejared.com' }
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings loom.layered {
		addLayer( quiltMappings.mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2") )
	}
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
	modImplementation "com.terraformersmc:modmenu:$modmenu_version"
	modImplementation "vazkii.patchouli:Patchouli:$patchouli_version-FABRIC"

	shade implementation( project(':lang-api') )
	shade implementation( project(':lang-dummy') )
//	shade compileOnly( project(':lang-lua') )  // uncomment to test lua

}

processResources {
	inputs.property 'version', project.version

	filesMatching('fabric.mod.json') {
		expand 'version': project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) and upwards uses Java 17.
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
	it.options.compilerArgs = [
		'--add-modules', 'jdk.incubator.foreign'
	] 
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present. If you remove this line, sources will not be generated.
	withSourcesJar()
}

shadowJar {
	configurations = [ project.configurations.shade ]
	from sourceSets.main.allSource
	classifier ''
}

jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		//noinspection GroovyAssignabilityCheck
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
	}
}
